#version 450 core

layout (local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

layout (std430, binding = 0) buffer position_buffer {
	vec4 positions[];
};
layout (std430, binding = 1) buffer color_buffer {
	vec4 colors[];
};
layout (std430, binding = 2) buffer velocity_buffer {
	vec4 velocities[];
};
layout (std430, binding = 3) buffer life_buffer {
	float lifes[];
};

uniform layout(location = 0) float dt;
uniform layout(location = 1) vec3 attractor;

float mid(float low, float val, float high)
{
    return max(low, min(val, high));
}

void main()
{
	uint id = gl_GlobalInvocationID.x;

    vec3 position = positions[id].xyz;
	vec3 velocity = velocities[id].xyz;
    vec4 color = colors[id];
    float life = lifes[id];

    if (life <= 0.0)
    {
        return;
    }

    vec3 acceleration = vec3(0.0);

    float attractor_distance = distance(attractor, position);
    if (attractor_distance >= 0.01)
    {
        acceleration = 5.0 * normalize(attractor - position) /
            max(attractor_distance, 0.5);
    }

    vec3 new_position = position + velocity * dt;
    vec3 new_velocity = velocity + acceleration * dt;
    new_velocity = min(2.0, length(new_velocity)) * normalize(new_velocity);

    float new_life = life - dt;

	positions[id].xyz = new_position;
    velocities[id].xyz = new_velocity;
    lifes[id] = new_life;
}
