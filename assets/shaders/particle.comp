#version 450 core

layout (local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

layout (std430, binding = 0) buffer position_buffer {
	vec4 positions[];
};
layout (std430, binding = 2) buffer velocity_buffer {
	vec4 velocities[];
};
layout (std430, binding = 3) buffer life_buffer {
	float lifes[];
};

uniform float dt;
uniform vec2 cursor;

float mid(float low, float val, float high)
{
    return max(low, min(val, high));
}

void main()
{
	uint id = gl_GlobalInvocationID.x;

    vec3 position = positions[id].xyz;
	vec3 velocity = velocities[id].xyz;
    float life = lifes[id];

    if (life <= 0.0)
    {
        return;
    }

    vec3 cursor3 = vec3(2.0 * cursor.x - 1.0, 2.0 * cursor.y - 1.0, 0.0);

    vec3 acceleration = vec3(0.0);

    float cursor_distance = distance(cursor3, position);
    if (cursor_distance >= 0.01)
    {
        acceleration = normalize(cursor3 - position) / max(cursor_distance, 0.5);
    }

    vec3 new_position = position + velocity * dt;
    vec3 new_velocity = velocity + acceleration * dt;
    new_velocity = min(2.0, length(new_velocity)) * normalize(new_velocity);

    new_position.x = mid(-1.0, new_position.x, 1.0);
    new_position.y = mid(-1.0, new_position.y, 1.0);

    float new_life = life - dt;

	positions[id].xyz = new_position;
    velocities[id].xyz = new_velocity;
    lifes[id] = new_life;
}
